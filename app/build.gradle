apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'

// build definitions & properties
def projectName = "places_search"

def env = System.getenv()
def projectEnv = new HashMap()
projectEnv.putAll(env)

if (true) { // TODO - Check if its not CI build.
    Properties placeSearchProperties = new Properties()
    placeSearchProperties.load(new FileInputStream(file('placessearch.properties')))
    updateProjectEnvVars(projectEnv, placeSearchProperties)
}

// Use all projectEnv as local variables
def foursquareapiclientid = projectEnv.get("foursquareapiclientid")
def foursquareapiclientsecret = projectEnv.get("foursquareapiclientsecret")
def userlocationlat = projectEnv.get("userlocationlat")
def userlocationlng = projectEnv.get("userlocationlng")

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    signingConfigs {
        // TODO update with release keystore info
        release {
            storeFile projectEnv.get("debugStoreFile") != null ? file(projectEnv.get("debugStoreFile")) : null
            keyAlias projectEnv.get("debugKeyAlias") != null ? projectEnv.get("debugKeyAlias") : ""
            storePassword projectEnv.get("debugStorePassword") != null ? projectEnv.get("debugStorePassword") : ""
            keyPassword projectEnv.get("debugKeyPassword") != null ? projectEnv.get("debugKeyPassword") : ""
        }
    }

    defaultConfig {
        applicationId "com.grace.placessearch"
        minSdkVersion 23
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Project/Build
        buildConfigField "String", "PROJECT_NAME", "\"${projectName}\""
        buildConfigField "String", "FOUR_SQUARE_API_CLIENT_ID", "\"${foursquareapiclientid}\""
        buildConfigField "String", "FOUR_SQUARE_API_CLIENT_SECRET", "\"${foursquareapiclientsecret}\""
        buildConfigField "String", "USER_LOCATION_LAT", "\"${userlocationlat}\""
        buildConfigField "String", "USER_LOCATION_LNG", "\"${userlocationlng}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles 'proguard-rules.pro'
            signingConfig signingConfigs.release
            debuggable false
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    // support libs
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:support-v4:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:customtabs:${supportLibVersion}"

    // maps
    compile ('com.google.android.gms:play-services-maps:12.0.1') {
        exclude group: 'com.android.support'
    }

    // Timber
    compile 'com.jakewharton.timber:timber:4.1.0'

    // Butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    // Dagger
    compile "com.google.dagger:dagger:2.6.1"
    apt "com.google.dagger:dagger-compiler:2.6.1"
    compile 'org.glassfish:javax.annotation:10.0-b28'

    // Gson
    compile 'com.google.code.gson:gson:2.3.1'

    // Picasso
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.0.2'

    // okhttp
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

    // Retrofit
    compile 'com.squareup.retrofit2:retrofit:2.0.2'
    compile 'com.squareup.retrofit2:converter-gson:2.0.2'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.0.2'

    // Rx
    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxandroid:1.1.0'

    // Test
    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:1.7.0'
}

// Store all build props as env props for ease of use
def updateProjectEnvVars(Map<String, String> projectEnv, Properties placesSearchProps) {
    try {

        projectEnv.put("foursquareapiclientid", placesSearchProps.getProperty("foursquareapiclientid"))
        projectEnv.put("foursquareapiclientsecret", placesSearchProps.getProperty("foursquareapiclientsecret"))
        projectEnv.put("userlocationlat", placesSearchProps.getProperty("userlocationlat"))
        projectEnv.put("userlocationlng", placesSearchProps.getProperty("userlocationlng"))
        projectEnv.put("debugStoreFile", placesSearchProps.getProperty("debugStoreFile"))
        projectEnv.put("debugKeyAlias", placesSearchProps.getProperty("debugKeyAlias"))
        projectEnv.put("debugStorePassword", placesSearchProps.getProperty("debugStorePassword"))
        projectEnv.put("debugKeyPassword", placesSearchProps.getProperty("debugKeyPassword"))

    } catch (Exception e) {
        println("updateProjectEnvVars - Unexpected Errors - Exception is " + e.getMessage())
        e.printStackTrace()
    }
}

